float x1;
float y1;

void setup(){
  size(2520,2520);
  colorMode(HSB,360,100,100,100);
  background(0);
  noStroke();
  noLoop();
}

void draw(){
  //背景のグラデーションを作る
  color c1 = color(181,17,97);
  color c2 = color(226,78,43);
  for(float i = 0; i < width; i += 5){
    for(float j = 0; j < height; j += 5){
      color c = lerpColor(c1,c2,(i+j)/(width+height));
      fill(c);
      rect(i,j,5,5);
    }
  }
  
  //クラゲの足みたいな曲線を作る
  noFill();
  stroke(181,17,97);
  strokeWeight(2);
  for(int i = 0; i < 6; i++){
    bezier(0,random(2000),random(2000),random(2000),random(2000),random(2000),2520,random(2000));
  }
  
  //シャボン玉を生成
  noStroke();
  for(int i = 0; i < 30; i ++){
    bubble(random(10,width-10),random(10,height-10),random(110,250));
  }
  
}

void bubble(float cx,float cy,float size){//シャボン玉関数
  int n = int(random(6));
  
  //ブルーのシャボン玉
  if(n == 1){
    fill(215,61,90,60);
    ellipse(cx,cy,size*2,size*2);
    for(int i = 0; i < 40; i++){
      float r = random(1);
      x1 = (size/1.5) * cos(radians(random(360))) * r + cx;
      y1 = (size/1.5) * sin(radians(random(360))) * r + cy;
      fill(143,17,90,90);
      rect(x1,y1,random(5,8),random(5,8));
    }
  }
  
  //ピンクのシャボン玉
  if(n == 2){
    fill(283,16,92,55);
    ellipse(cx,cy,size*2,size*2);
    for(int i = 0; i < 5; i ++){
      float r = random(1);
      x1 = (size/1.5) * cos(radians(random(360))) * r + cx;
      y1 = (size/1.5) * sin(radians(random(360))) * r + cy;
      flower(x1,y1);
    }
  }
  
  //基本のシャボン玉(ブルーにもピンクにも重ねる)
  for(int i = 0; i < 80; i++){
    for(int angle = 0 + i; angle <= 360 + i; angle++){
      x1 = (size-i) * cos(radians(angle));
      y1 = (size-i) * sin(radians(angle));
    
      fill(angle,60,100,100-(i*2));
      ellipse(cx+x1,cy+y1,1,1);
    }
  }
  
  //小さな円(泡)を作る
  fill(181,17,97,10);
  for(int i = 0; i < 8; i ++){
    float s = random(10,110);
      ellipse(random(width),random(height),s,s);
    }
  
}

//ピンク色のシャボン玉に入れる花を作る
void flower(float x2,float y2){
  stroke(36,65,74,60);
  strokeWeight(3);
  noFill();
  ellipse(x2,y2+10,40,40);
  ellipse(x2+10,y2,40,40);
  ellipse(x2-10,y2,40,40);
  ellipse(x2,y2-10,40,40);
  noStroke();
}

//画像保存
void keyPressed(){
  if(key == ENTER){
    save("pcd2021_eboshidori.png");
  }
}
